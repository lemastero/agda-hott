{-# OPTIONS --without-K --exact-split --safe --no-unicode #-}

module FP.Maybe where

open import TypeTheory.Universes using (Type; ð‘¢)
open import TypeTheory.SimpleTypes using (id; _compose_)
open import HoTT.Identity-Types using (_â‰¡_; refl)

data Maybe (A : Type ð‘¢) : Type ð‘¢ where
  Just : (x : A) -> Maybe A
  None :            Maybe A

-- operations

map-Maybe : {A B : Type ð‘¢} -> (A -> B) -> Maybe A -> Maybe B
map-Maybe f (Just a) = Just (f a)
map-Maybe f None     = None

flatMap-Maybe : {A B : Type ð‘¢} -> Maybe A -> (A -> Maybe B) -> Maybe B
flatMap-Maybe (Just a) f = f a
flatMap-Maybe None     f = None

-- propos of map

maybe-map-id : {A : Type ð‘¢} (fa : Maybe A)
            -> map-Maybe id fa â‰¡ fa
maybe-map-id (Just a) = refl (Just a)
maybe-map-id None     = refl None

maybe-map-compose : {A B C : Type ð‘¢}
                 -> (f : A -> B)
                 -> (g : B -> C)
                 -> (fa : Maybe A)
                 -> map-Maybe (g compose f) fa â‰¡ map-Maybe g (map-Maybe f fa)
maybe-map-compose f g (Just x) = refl (Just (g (f x)))
maybe-map-compose f g None     = refl None

-- propos of flatMap

maybe-flatMap-just-f : {A B : Type ð‘¢} (a : A) (f : A -> Maybe B)
            -> flatMap-Maybe (Just a) f â‰¡ f a
maybe-flatMap-just-f a fab = refl (fab a)

maybe-flatMap-f-just : {A : Type ð‘¢} (fa : Maybe A) -> flatMap-Maybe fa Just â‰¡ fa
maybe-flatMap-f-just (Just x) = refl (Just x)
maybe-flatMap-f-just None     = refl None

maybe-flatMap-compose : {A B C : Type ð‘¢}
  -> (f : A -> Maybe B)
  -> (g : B -> Maybe C)
  -> (fa : Maybe A)
  -> flatMap-Maybe (flatMap-Maybe fa f) g â‰¡
     flatMap-Maybe fa (\ x -> flatMap-Maybe (f x) g)
maybe-flatMap-compose fab fbc (Just x) = refl (flatMap-Maybe (fab x) fbc)
maybe-flatMap-compose fab fbc None = refl None
